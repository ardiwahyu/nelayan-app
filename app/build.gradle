plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null
    }
}

def getVersionCode = { ->
    try {
        def code = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'tag', '--list'
            standardOutput = code
        }
        return code.toString().split("\n").size()
    }
    catch (ignored) {
        return 1
    }
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.1"

    defaultConfig {
        applicationId "com.github.nelayanapp"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        versionCode getVersionCode()
        versionName getVersionName()
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    androidExtensions {
        experimental = true
    }
    lintOptions {
        abortOnError false
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    testOptions {
        unitTests.includeAndroidResources = true
    }
    buildFeatures{
        dataBinding = true
        viewBinding = true
    }
    flavorDimensions "version"
    productFlavors {
        prod {
            dimension "version"
            buildConfigField "String", "BASE_URL", '""'
            buildConfigField "Integer", "CLIENT_ID", '3'
            buildConfigField "String", "CLIENT_SECRET", '""'
            resValue "string", "app_name", "Nelayan App"
        }
        dev {
            dimension "version"
            buildConfigField "String", "BASE_URL", '""'
            buildConfigField "Integer", "CLIENT_ID", '0'
            buildConfigField "String", "CLIENT_SECRET", '""'
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            resValue "string", "app_name", "Nelayan App Devel"
        }
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.drawerlayout:drawerlayout:1.1.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.android.support:multidex:1.0.3'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.1'

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:26.5.0')
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-messaging-ktx'

    // Dependency Injection (Hilt)
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha02"
    kapt 'androidx.hilt:hilt-compiler:1.0.0-alpha02'

    // Kotlin Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.1'

    // Lifecycle (ViewModel & LiveData)
    def lifecycle_version = "2.3.0"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    // Anko
    def anko_version = "0.10.8"
    implementation "org.jetbrains.anko:anko:$anko_version"

    // Room DB
    def room_version = "2.2.6"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    // Retrofit
    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'

    //  event bus
    implementation 'org.greenrobot:eventbus:3.2.0'

    // Glide library
    def glide_version = "4.11.0"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    implementation('com.github.bumptech.glide:okhttp3-integration:4.11.0') {
        exclude group: 'glide-parent'
    }
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    // debugging
    def timberVersion = "4.7.1"
    def stethoVersion = "1.5.1"
    implementation "com.jakewharton.timber:timber:$timberVersion"
    implementation "com.facebook.stetho:stetho:$stethoVersion"
    implementation "com.facebook.stetho:stetho-okhttp3:$stethoVersion"
    implementation "io.sentry:sentry-android:1.7.27"
    implementation 'org.slf4j:slf4j-nop:1.7.30'

    // android debug database
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'

    implementation 'com.synnapps:carouselview:0.1.5'
    implementation 'com.tbuonomo.andrui:viewpagerdotsindicator:4.1.2'

    // Google Play Services
    implementation 'com.google.android.gms:play-services-location:17.1.0'
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'com.google.android.libraries.places:places:2.4.0'
    implementation 'com.google.maps:google-maps-services:0.9.3'
    implementation 'com.google.maps.android:android-maps-utils:0.5'

    // Joda & Koda time library
    implementation 'joda-time:joda-time:2.10.1'
    implementation 'com.github.debop:koda-time:1.2.1'

    implementation 'com.github.markushi:circlebutton:1.1'
    implementation 'br.com.simplepass:loading-button-android:2.2.0'

    // Navigation component
    def nav_version = "2.3.3"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    implementation 'com.facebook.shimmer:shimmer:0.1.0@aar'
    implementation 'io.fotoapparat:fotoapparat:2.7.0'

    implementation 'net.cachapa.expandablelayout:expandablelayout:2.9.2'

    implementation 'com.droidninja:filepicker:2.2.5'
    implementation 'com.jsibbold:zoomage:1.3.1'

    implementation 'com.savvi.datepicker:rangepicker:1.0.1'
}